import org.moallemi.gradle.internal.VersionCodeType

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'org.moallemi.advanced-build-version'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.github.gfx.ribbonizer'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'io.fabric'

def signingProps = new Properties()
signingProps.load(new FileInputStream(rootProject.file('signing.properties')))

advancedVersioning {
    nameOptions {
        versionMajor 1
        versionMinor 0
        versionPatch 0
    }

    codeOptions {
        versionCodeType VersionCodeType.AUTO_INCREMENT_ONE_STEP
    }

    outputOptions {
        renameOutput true
    }
}

android {
    signingConfigs {
        release {
            storeFile rootProject.file(signingProps.keyStore)
            storePassword signingProps.keyStorePassword
            keyAlias signingProps.keyAlias
            keyPassword signingProps.keyAliasPassword
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.aluxian.codementor"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode advancedVersioning.versionCode
        versionName advancedVersioning.versionName
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            debuggable true
        }

        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:recyclerview-v7:23.1.0'
    compile 'com.android.support:cardview-v7:23.1.0'
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile 'com.firebase:firebase-client-android:2.4.0'
    compile 'com.parse.bolts:bolts-android:1.2.1'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.facebook.fresco:fresco:0.8.1'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.google.guava:guava:18.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.2@aar') { transitive = true; }
    apt 'com.bluelinelabs:logansquare-compiler:1.1.0'
    compile 'com.bluelinelabs:logansquare:1.1.0'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
}

ribbonizer {
    builder { variant, iconFile ->
        return grayRibbonFilter(variant, iconFile)
    }
}
